// Copyright 2018 Intel Corporation
// SPDX-License-Identifier: Apache-2.0

#pragma once

#include "tensor_accessor.isph"

struct CPUUpsampleKernel
{
  uniform TensorAccessor3D src;
  uniform TensorAccessor3D dst;
};

template<typename T>
inline void CPUUpsampleKernel_run(const uniform CPUUpsampleKernel* uniform self,
                                  uniform int cb, uniform int h)
{
  const uniform int B = getBlockC<T>();
  const uniform size_t W = (uniform size_t)self->src.W;

  uniform T* const uniform srcPtr_line  = (uniform T* const uniform)Tensor_getPtr<T>(self->src, cb*B, h, 0);
  uniform T* const uniform dstPtr_line0 = (uniform T* const uniform)Tensor_getPtr<T>(self->dst, cb*B, h*2, 0);
  uniform T* const uniform dstPtr_line1 = (uniform T* const uniform)Tensor_getPtr<T>(self->dst, cb*B, h*2+1, 0);

  for (uniform size_t w = 0; w < W; ++w)
  {
    const T value = *((varying T* uniform)&srcPtr_line[w*B]);

    streaming_store(&dstPtr_line0[w*2*B  ], value);
    streaming_store(&dstPtr_line0[w*2*B+B], value);
    streaming_store(&dstPtr_line1[w*2*B  ], value);
    streaming_store(&dstPtr_line1[w*2*B+B], value);
  }
}